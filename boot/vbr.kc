# adapted BootProg boot32.asm

ORG 0
USE16

RB 3
bsOemName: RB 8

bpbBytesPerSector:    RB 2
bpbSectorsPerCluster: RB 1
bpbReservedSectors:   RB 2
bpbNumberOfFATs:      RB 1
bpbRootEntries:       RB 2
bpbTotalSectors:      RB 2
bpbMedia:             RB 1
bpbSectorsPerFAT:     RB 2
bpbSectorsPerTrack:   RB 2
bpbHeadsPerCylinder:  RB 2
bpbHiddenSectors:     RB 4
bpbTotalSectorsBig:   RB 4

bsSectorsPerFAT32:        RB 4
bsExtendedFlags:          RB 2
bsFSVersion:              RB 2
bsRootDirectoryClusterNo: RB 4
bsFSInfoSectorNo:         RB 2
bsBackupBootSectorNo:     RB 2
bsreserved:               RB 12
bsDriveNumber:           RB 1
bsreserved1:              RB 1
bsExtendedBootSignature:  RB 1
bsVolumeSerialNumber:     RB 4
bsVolumeLabel:            RB 11
bsFileSystemName:         RB 8

start:
    CLD
    MOV CX, DI

    INT 0x12
    SHL AX, 6
    SUB AX, (512 + 2048) /! 16
    MOV ES, AX
    MOV SS, AX
    MOV SP, 512 + 2048

    MOV CX, 256
    MOV SI, 0x7C00
    XOR DI, DI
    MOV DS, DI
    REP MOVSW

    PUSH ES
    PUSH main
    RETF

main:
    PUSH CS
    POP DS

    MOV [bsDriveNumber], DL

    PUSH 0x100
    POP ES

    AND BYTE [bsRootDirectoryClusterNo + 3], 0x0F
    MOV EBP, [bsRootDirectoryClusterNo]

RootDirReadContinue:
    PUSH ES
    CALL ReadCluster
    POP ES
    PUSHF

    XOR DI, DI

    MOVZX AX, BYTE [bpbSectorsPerCluster]
    MUL WORD [bpbBytesPerSector]

FindName:
    MOV SI, ProgramName
    MOV CL, 11
    CMP BYTE [ES:DI], CH
    JZ SHORT FindNameFailed
    PUSH DI
    REP CMPSB
    POP DI
    JZ SHORT FindNameFound
    ADD DI, 32
    CMP DI, AX
    JNZ SHORT FindName
    POPF
    JB SHORT RootDirReadContinue
FindNameFailed:
    CALL Error
    DB 'NoFile', 0
FindNameFound:
    PUSH WORD [ES:DI + 0x14]
    PUSH WORD [ES:DI + 0x1A]
    POP EBP

    PUSH ES

FileReadContinue:
    CALL ReadCluster
    JB SHORT FileReadContinue

    MOV BL, [bsDriveNumber]
    MOV EDX, [bpbHiddenSectors]

    XOR AX, AX
    MOV ES, AX
    MOV DS, AX
    MOV SS, AX
    MOV SP, 0x1000
    PUSH 0
    PUSH 0x1000
    RETF

ReadCluster:
    LEA ESI, [EBP + -2]

    MOV AX, [bpbBytesPerSector]
    SHR AX, 2
    CWDE
    XCHG EAX, EBP
    CDQ
    DIV EBP

    MOV CX, 1
    PUSH ES
    CALL ReadSector
    POP ES

    IMUL DI, DX, 4
    AND BYTE [ES:DI + 3], 0x0F
    MOV EBP, [ES:DI]

    MOVZX EAX, BYTE [bpbNumberOfFATs]
    MUL DWORD [bsSectorsPerFAT32]

    XCHG EAX, ESI

    MOVZX ECX, BYTE [bpbSectorsPerCluster]
    MUL ECX

    ADD EAX, ESI

ReadSector:
    MOV DI, 5

ReadSectorRetry:
    PUSHAD
    
    MOVZX EDX, WORD [bpbReservedSectors]
    ADD EAX, EDX

    XOR DX, DX
    ADD EAX, [bpbHiddenSectors]
    ADC DX, 0

    PUSH EDX
    PUSH EAX
    PUSH ES
    PUSH 0
    PUSH 1
    PUSH 16

    MOV SI, SP
    MOV AH, 0x42

    MOV DL, [bsDriveNumber]

    INT 0x13

    LEA SP, [SI + 16]

    JNB SHORT ReadSectorDone

    MOV AH, 0

    INT 0x13

    POPAD

    DEC DI
    JNZ SHORT ReadSectorRetry

ReadError:
    CALL Error
    DB 'ReadErr', 0

ReadSectorDone:
    MOV DX, [bpbBytesPerSector]
    SHR DX, 4
    MOV AX, ES
    ADD AX, DX
    MOV ES, AX

    POPAD

    INC EAX

    LOOP ReadSector

    CMP EBP, 0xFFFFFF8

    RET

Error:
    POP SI
    MOV AH, 0x0E
    MOV BX, 7

ErrorNext:
    LODSB
Halt:
    HLT
    TEST AL, AL
    JZ SHORT Halt
    INT 0x10
    JMP ErrorNext

ProgramName: DB 'EDEN    EXE'