include "lib/def.kc"
include "lib/util.kc"
include "lib/fs.kc"
include "lib/malloc.kc"
include "lib/map.kc"
include "lib/vec.kc"
include "lib/img.kc"
include "compiler/compiler.kc"
include "boot/task.kc"
include "adam/task.kc"
include "adam/vbe.kc"
include "adam/font.kc"
include "adam/keyboard.kc"
include "adam/window.kc"
include "adam/mouse.kc"
include "adam/pci.kc"
include "net/init.kc"
include "sound/init.kc"

function eve(compiler)
(c{4}, olm, orm, omm, w, k, m{MOUSE_EVENT}, dragging, startx, starty, ox, oy, hover_toggle)
{
    CLI
    adam_key_events = malloc(VEC);
    adam_mouse_events = malloc(VEC);
    vec_init(adam_key_events, 64, 1);
    vec_init(adam_mouse_events, 64, 4);
    STI
    set_resolution(1024, 768);
    new_task("Wallpaper Loader", set_wallpaper, "/adam/wallpape.qoi");
    init_mouse();
    create_window(8, 8, 512, 128);

    print("
i i i i i i i      mmmmmmm   mm   m      m    m m    m mmmm  
I I I I I I I         #      ##   #      ##  ## #    # #   \"m
I  \\ `+' /  I         #     #  #  #      # ## # #    # #    #
 \\  `-+-'  /          #     #mm#  #      # \"\" # #    # #    #
  `-__|__-'           #    #    # #mmmmm #    # \"mmmm\" #mmm\" 
      |
------+------

Welcome to TalmudOS (Formerly ChucksOS)
");

    rand_state = cmos_read(0) + cmos_read(2) *! 0x60 + cmos_read(4) *! 0x60 *! 0x60 + cmos_read(7) *! 0x60 *! 0x60 *! 0x24;

    new_task("Seth", seth, 0);
    new_task("Networking", net_init, 0);
    new_task("Sound", sound_init, 0);

    draw_cursor();
    dragging = NULL;
    olm = FALSE;
    orm = FALSE;
    omm = FALSE;
    hover_toggle = NULL;
    while (TRUE)
    {
        if (adam_key_events.[VEC_LEN] == 0 and adam_mouse_events.[VEC_LEN] == 0)
        {
            wait();
        }
        else if (adam_key_events.[VEC_LEN])
        {
            CLI
            vec_shift(adam_key_events, c);
            STI
            k = 0;
            if (c{0} == 0xE0)
            {
                while (adam_key_events.[VEC_LEN] == 0) wait();
                CLI
                vec_shift(adam_key_events, c);
                STI
                k = 0x100;
            }
            k |= c{0};
            if (k & 0x80) k ^= K_RELEASED | (c{0} & 0x80);
            if (k == K_LSHIFT or k == K_RSHIFT) shift_pressed = TRUE;
            else if (k == (K_LSHIFT | K_RELEASED) or k == (K_RSHIFT | K_RELEASED)) shift_pressed = FALSE;
            if (k == K_LCTRL) ctrl_pressed = TRUE;
            else if (k == (K_LCTRL | K_RELEASED)) ctrl_pressed = FALSE;
            if (k == K_LALT) alt_pressed = TRUE;
            else if (k == (K_LALT | K_RELEASED)) alt_pressed = TRUE;

            if (k == K_T and ctrl_pressed and alt_pressed)
            {
                new_task("Seth", seth, 0);
            }
            else if (k == K_PGUP and windows{WINDOW_FLAGS} & WINDOW_FPRINT_SCROLL)
            {
                window_print_scroll_up(windows, windows.[WINDOW_H] /! 8);
                windows.[WINDOW_TASK]{TASK_WAIT} = FALSE;
            }
            else if (k == K_PGDOWN and windows{WINDOW_FLAGS} & WINDOW_FPRINT_SCROLL)
            {
                window_print_scroll_down(windows, windows.[WINDOW_H] /! 8);
                windows.[WINDOW_TASK]{TASK_WAIT} = FALSE;
            }
            else if (windows{WINDOW_FLAGS} & WINDOW_FPRINT_SCROLL and (k == (K_PGUP & K_RELEASED) or k == (K_PGDOWN & K_RELEASED)))
            {
                # ignore
            }
            else if (windows{WINDOW_FLAGS} & WINDOW_FGET_KEY_EVENTS)
            {
                vec_push2(&windows{WINDOW_KEY_EVENTS}, k);
                windows.[WINDOW_TASK]{TASK_WAIT} = FALSE;
            }
        }
        else
        {
            CLI
            vec_shift(adam_mouse_events, c);
            STI
            m.[MOUSE_X] = (c{0} & 16) ? ~0xFF | c{1} : c{1};
            if (!mouse_captured) cursor_x += m.[MOUSE_X];
            if (cursor_x <$ 0) cursor_x = 0;
            if (cursor_x >=$ fb_width) cursor_x = fb_width - 1;
            m.[MOUSE_Y] = -((c{0} & 32) ? ~0xFF | c{2} : c{2});
            if (!mouse_captured) cursor_y += m.[MOUSE_Y];
            if (cursor_y <$ 0) cursor_y = 0;
            if (cursor_y >=$ fb_height) cursor_y = fb_height - 1;
            draw_cursor();
            m{MOUSE_SCROLL} = c{3} & 0xF;

            if (mouse_captured)
            {
                w = mouse_captured;
            }
            else if (dragging)
            {
                w = dragging;
            }
            else
            {
                w = windows;
                while (w and (w.[WINDOW_X] >$ cursor_x or cursor_x >=$ w.[WINDOW_X] + w.[WINDOW_W] or w.[WINDOW_Y] >$ cursor_y or cursor_y >=$ w.[WINDOW_Y] + w.[WINDOW_H] + TITLEBAR_SIZE))
                {
                    w = w.[WINDOW_BELOW];
                }
            }
            if (hover_toggle and hover_toggle != w)
            {
                window_draw_titlebar(hover_toggle);
                hover_toggle = NULL;
            }
            if (w)
            {
                m{MOUSE_FLAGS} = c{0} & 1 and !olm and MOUSE_LMB_DOWN;
                m{MOUSE_FLAGS} |= c{0} & 2 and !orm and MOUSE_RMB_DOWN;
                m{MOUSE_FLAGS} |= c{0} & 4 and !omm and MOUSE_MMB_DOWN;
                m{MOUSE_FLAGS} |= !(c{0} & 1) and olm and MOUSE_LMB_UP;
                m{MOUSE_FLAGS} |= !(c{0} & 2) and orm and MOUSE_RMB_UP;
                m{MOUSE_FLAGS} |= !(c{0} & 4) and omm and MOUSE_MMB_UP;
                if (m{MOUSE_FLAGS} & (MOUSE_LMB_DOWN | MOUSE_RMB_DOWN | MOUSE_MMB_DOWN)) focus_window(w);
                if (dragging or cursor_y <$ w.[WINDOW_Y] + TITLEBAR_SIZE)
                {
                    if (cursor_x >=$ w.[WINDOW_X] + w.[WINDOW_W] - 16 and !dragging)
                    {
                        if (m{MOUSE_FLAGS} & MOUSE_LMB_DOWN)
                        {
                            if (w.[WINDOW_TASK] == task)
                            {
                                print("Ouch!\n");
                                JMP .l
                            }
                            hover_toggle = NULL;
                            kill(w.[WINDOW_TASK]);
                            JMP .l
                        }
                        if (!hover_toggle) window_draw_titlebar(w);
                        hover_toggle = w;
                        JMP .s
                    }
                    else if (hover_toggle)
                    {
                        window_draw_titlebar(w);
                        hover_toggle = NULL;
                    }
                    
                    if (m{MOUSE_FLAGS} & MOUSE_LMB_DOWN)
                    {
                        dragging = w;
                        startx = cursor_x;
                        starty = cursor_y;
                    }
                    else if (m{MOUSE_FLAGS} & MOUSE_LMB_UP)
                    {
                        dragging = NULL;
                    }
                    else if (dragging)
                    {
                        ox = w.[WINDOW_X];
                        oy = w.[WINDOW_Y];
                        w.[WINDOW_X] += cursor_x - startx;
                        w.[WINDOW_Y] += cursor_y - starty;
                        framebuffer_refresh(mins(w.[WINDOW_X], ox), mins(w.[WINDOW_Y], oy), w.[WINDOW_W] + abs(ox - w.[WINDOW_X]), w.[WINDOW_H] + TITLEBAR_SIZE + abs(oy - w.[WINDOW_Y]), NULL);
                        startx = cursor_x;
                        starty = cursor_y;
                    }
                }
                else if (w{WINDOW_FLAGS} & WINDOW_FGET_MOUSE_EVENTS and (mouse_captured or (w.[WINDOW_Y] + TITLEBAR_SIZE <$ cursor_y)))
                {
                    if (hover_toggle) window_draw_titlebar(w);
                    hover_toggle = NULL;
                    if (!mouse_captured)
                    {
                        m.[MOUSE_X] = cursor_x - w.[WINDOW_X];
                        m.[MOUSE_Y] = cursor_y - w.[WINDOW_Y] - TITLEBAR_SIZE;
                    }
                    vec_push(&w{WINDOW_MOUSE_EVENTS}, m);
                    windows.[WINDOW_TASK]{TASK_WAIT} = FALSE;
                }
                else if (hover_toggle)
                {
                    window_draw_titlebar(w);
                    hover_toggle = NULL;
                }
.s:
                if (w{WINDOW_FLAGS} & WINDOW_FPRINT_SCROLL)
                {
                    if (m{MOUSE_SCROLL} == MOUSE_SCROLL_DOWN) window_print_scroll_up(w, 1);
                    else if (m{MOUSE_SCROLL} == MOUSE_SCROLL_UP) window_print_scroll_down(w, 1);
                    else JMP .l
                    w.[WINDOW_TASK]{TASK_WAIT} = FALSE;
                }
            }
            
.l:
            olm = c{0} & 1;
            orm = c{0} & 2;
            omm = c{0} & 4;
        }
        next_task();
    }
}

function seth()
(s{STRING}, p{VEC}, x, t, i, ans)
{
    create_window(200, 200, 640, 480){WINDOW_FLAGS} = WINDOW_FGET_KEY_EVENTS | WINDOW_FPRINT_SCROLL;
    compiler_global_set(&task{TASK_COMPILER}, "ans", &ans);
    vec_init(p, 128, 1);
    s.[STRINGLEN] = 0;
    while (TRUE)
    {
.l:
        if (s.[STRINGLEN] == 0)
        {
            dir2path(p, &task{TASK_CWD});
            format(print, "%s> ", p);
            t = task.[TASK_WINDOW].[WINDOW_PRINT_BUFFER + VEC_LEN];
        }
        i = input(s, t, TRUE);
        if (!i)
        {
            clear();
            s.[STRINGLEN] = 0;
            JMP .l
        }
        x = compile(&task{TASK_COMPILER}, "stdin", i);
        if (x)
        {
            s.[STRINGLEN] = 0;
            ans = x();
            format(print, "ans=%d (%x)\n", ans, ans);
        }
        else
        {
            if (task.[TASK_COMPILER + CCUR + TOKEN_TYPE] != TEND) s.[STRINGLEN] = 0;
        }
    }
}

function dir(p)
(f{FILE}, d{DIR_ENTRY}, s{STRING})
{
    if (!open(p, f, 0)) return format(print, "Could not open path \"%s\"\n", p);
    if (!(f{FILE_FLAGS} & FILE_FDIRECTORY)) return print("Not a directory\n");
    s.[STRINGLEN] = 11;
    s.[STRINGPTR] = &d{DIR_ENTRY_83NAME};
    while (dir_next(f, d))
    {
        format(print, "%s", s);
        if (!(d{DIR_ENTRY_ATTR} & FILE_FDIRECTORY)) format(print, " %dK", d.[DIR_ENTRY_FILE_SIZE] /! 1024);
        print("\n");
    }
}

function memrep()
(m{MAP}, i, freemem, usedmem, n)
{
    map_init(m, 8, 4, 4, intcmp, hashint, NULL);
    i = first_free;
    freemem = 0;
    usedmem = 0;
    while (i)
    {
        if (!(i{MEM_HEADER_FLAGS} & MEM_HEADER_FUSED))
        {
            freemem += i.[MEM_HEADER_SIZE];
        }
        else
        {
            usedmem += i.[MEM_HEADER_SIZE];
            if (map_get(m, &i{MEM_HEADER_TASK}, &n))
            {
                n += i.[MEM_HEADER_SIZE];
                map_set(m, &i{MEM_HEADER_TASK}, &n);
            }
            else
            {
                map_set(m, &i{MEM_HEADER_TASK}, &i.[MEM_HEADER_SIZE]);
            }
        }
        i = i.[MEM_HEADER_NEXT];
    }
    format(print, "%dM free, %dK used, %dK reserved for YHWH\n", freemem /! 1024 /! 1024, usedmem /! 1024, (first_free - 0x500) /! 1024);
    n = 0;
    while (map_iter(m, &n))
    {
        format(print, "%dK used by task %s\n", n[1] /! 1024, &n[0]{TASK_NAME});
    }
    map_free(m);
}

function taskrep()
(t, x, m{MAP}, r)
{
    map_init(m, 8, 4, 4, intcmp, hashint, NULL);
    x = timer_ms;
    t = task;
    do
    {
        map_set(m, &t, &t.[TASK_RUNTIME]);
        t = t.[TASK_NEXT];
    } while (t != task);
    sleep(1000);
    do
    {
        if (map_get(m, &t, &r))
        {
            r = ((t.[TASK_RUNTIME] - r) *! 10000) /! (timer_ms - x);
            format(print, "%s (%x) %d.%d%%\n", &t{TASK_NAME}, t, r /! 100, r %! 100);
        }
        t = t.[TASK_NEXT];
    } while (t != task);
    map_free(m);
}

function clear()
{
    task.[TASK_WINDOW].[WINDOW_PRINT_X] = 0;
    task.[TASK_WINDOW].[WINDOW_PRINT_Y] = 0;
    task.[TASK_WINDOW].[WINDOW_PRINT_CURSOR] = 0;
    task.[TASK_WINDOW].[WINDOW_PRINT_SCROLLTOP] = 0;
    vec_free(&task.[TASK_WINDOW]{WINDOW_PRINT_BUFFER});
    vec_init(&task.[TASK_WINDOW]{WINDOW_PRINT_BUFFER}, 128, 1);
    fill_rect(0, 0, task.[TASK_WINDOW].[WINDOW_W], task.[TASK_WINDOW].[WINDOW_H], 0xFFFFFF);
}

function edit(p)
(f{FILE}, s{STRING}, b)
{
    clear();
    if (!open(p, f, FILE_FCREATE)) return print("Could not open file\n");
    if (!vec_resize(&task.[TASK_WINDOW]{WINDOW_PRINT_BUFFER}, f.[FILE_SIZE]) or read(f, f.[FILE_SIZE], task.[TASK_WINDOW].[WINDOW_PRINT_BUFFER + VEC_PTR]) != f.[FILE_SIZE]) return print("Could not read file\n");
    b = malloc(task.[TASK_NAME + STRINGLEN] + p.[STRINGLEN] + 3);
    memcpy(b, task.[TASK_NAME + STRINGPTR], task.[TASK_NAME + STRINGLEN]);
    memcpy(&b{task.[TASK_NAME + STRINGLEN]}, " - ".[STRINGPTR], 3);
    memcpy(&b{task.[TASK_NAME + STRINGLEN] + 3}, p.[STRINGPTR], p.[STRINGLEN]);
    free(task.[TASK_NAME + STRINGPTR]);
    task.[TASK_NAME + STRINGPTR] = b;
    task.[TASK_NAME + STRINGLEN] += p.[STRINGLEN] + 3;
    draw_titlebar();
    task.[TASK_WINDOW].[WINDOW_PRINT_BUFFER + VEC_LEN] = f.[FILE_SIZE];
    window_print_redraw(task.[TASK_WINDOW]);
    while (input(s, 0, TRUE));
    seek(f, 0);
    write(f, s.[STRINGLEN], s.[STRINGPTR]);
    resize(f, s.[STRINGLEN]);
    clear();
    task.[TASK_NAME + STRINGLEN] -= p.[STRINGLEN] + 3;
    draw_titlebar();
}

reboot:
    MOV EAX, 2
.r:
    IN AL, 0x64
    TEST AL, 2
    JNZ .r
    MOV AL, 0xFE
    OUT 0x64, AL
    CLI
.h: HLT
    JMP .h

function screenshot_rect(p, x, y, w, h) return save_qoi(p, &framebuffer{y *! fb_pitch + x *! 4}, w, h, fb_pitch, 3, 0);
function screenshot(p) return screenshot_rect(p, 0, 0, fb_width, fb_height);