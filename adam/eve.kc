include "lib/def.kc"
include "lib/util.kc"
include "lib/fs.kc"
include "lib/malloc.kc"
include "lib/map.kc"
include "lib/vec.kc"
include "compiler/compiler.kc"
include "boot/task.kc"
include "adam/task.kc"
include "adam/vbe.kc"
include "adam/font.kc"
include "adam/keyboard.kc"
include "adam/window.kc"
include "adam/mouse.kc"

global ans;

function eve(compiler)
(c{3}, olm, orm, omm, w, k, m{MOUSE_EVENT}, dragging, startx, starty, ox, oy)
{
    adam_key_events = malloc(VEC);
    adam_mouse_events = malloc(VEC);
    vec_init(adam_key_events, 64, 1);
    vec_init(adam_mouse_events, 64, 3);
    set_resolution(1024, 768);
    init_mouse();
    create_window(8, 8, 512, 128);

    print("
i i i i i i i      mmmmmmm   mm   m      m    m m    m mmmm  
I I I I I I I         #      ##   #      ##  ## #    # #   \"m
I  \\ `+' /  I         #     #  #  #      # ## # #    # #    #
 \\  `-+-'  /          #     #mm#  #      # \"\" # #    # #    #
  `-__|__-'           #    #    # #mmmmm #    # \"mmmm\" #mmm\" 
      |
------+------

Welcome to TalmudOS (Formerly ChucksOS)
");

    new_task("Seth", seth, 0);

    draw_cursor();
    dragging = NULL;
    olm = FALSE;
    orm = FALSE;
    omm = FALSE;
    while (TRUE)
    {
        if (adam_key_events.[VEC_LEN] == 0 and adam_mouse_events.[VEC_LEN] == 0)
        {
            wait();
        }
        else if (adam_key_events.[VEC_LEN])
        {
            CLI
            vec_shift(adam_key_events, c);
            STI
            k = 0;
            if (c{0} == 0xE0)
            {
                while (adam_key_events.[VEC_LEN] == 0) wait();
                CLI
                vec_shift(adam_key_events, c);
                STI
                k = 0x100;
            }
            k |= c{0};
            if (k & 0x80) k ^= K_RELEASED | (c{0} & 0x80);
            if (k == K_LSHIFT or k == K_RSHIFT) shift_pressed = TRUE;
            else if (k == (K_LSHIFT | K_RELEASED) or k == (K_RSHIFT | K_RELEASED)) shift_pressed = FALSE;
            if (k == K_LCTRL) ctrl_pressed = TRUE;
            else if (k == (K_LCTRL | K_RELEASED)) ctrl_pressed = FALSE;
            if (k == K_LALT) alt_pressed = TRUE;
            else if (k == (K_LALT | K_RELEASED)) alt_pressed = TRUE;

            if (k == K_T and ctrl_pressed and alt_pressed)
            {
                new_task("Seth", seth, 0);
            }
            else if (k == K_PGUP and windows{WINDOW_FLAGS} & WINDOW_FPRINT_SCROLL)
            {
                window_print_scroll_up(windows);
            }
            else if (k == K_PGDOWN and windows{WINDOW_FLAGS} & WINDOW_FPRINT_SCROLL)
            {
                window_print_scroll_down(windows);
            }
            else if (windows{WINDOW_FLAGS} & WINDOW_FPRINT_SCROLL and (k == (K_PGUP & K_RELEASED) or k == (K_PGDOWN & K_RELEASED)))
            {
                # ignore
            }
            else if (windows{WINDOW_FLAGS} & WINDOW_FGET_KEY_EVENTS)
            {
                vec_push2(&windows{WINDOW_KEY_EVENTS}, k);
                windows.[WINDOW_TASK].[TASK_WAIT] = FALSE;
            }
        }
        else
        {
            CLI
            vec_shift(adam_mouse_events, c);
            STI
            m.[MOUSE_X] = (c{0} & 16) ? ~0xFF | c{1} : c{1};
            if (!mouse_captured) cursor_x += m.[MOUSE_X];
            if (cursor_x <$ 0) cursor_x = 0;
            if (cursor_x >=$ fb_width) cursor_x = fb_width - 1;
            m.[MOUSE_Y] = -((c{0} & 32) ? ~0xFF | c{2} : c{2});
            if (!mouse_captured) cursor_y += m.[MOUSE_Y];
            if (cursor_y <$ 0) cursor_y = 0;
            if (cursor_y >=$ fb_height) cursor_y = fb_height - 1;
            draw_cursor();

            if (mouse_captured)
            {
                w = mouse_captured;
            }
            else if (dragging)
            {
                w = dragging;
            }
            else
            {
                w = windows;
                while (w and (w.[WINDOW_X] >$ cursor_x or cursor_x >$ w.[WINDOW_X] + w.[WINDOW_W] or w.[WINDOW_Y] >$ cursor_y or cursor_y >$ w.[WINDOW_Y] + w.[WINDOW_H] + TITLEBAR_SIZE))
                {
                    w = w.[WINDOW_BELOW];
                }
            }
            if (w)
            {
                m{MOUSE_FLAGS} = c{0} & 1 and !olm and MOUSE_LMB_DOWN;
                m{MOUSE_FLAGS} |= c{0} & 2 and !orm and MOUSE_RMB_DOWN;
                m{MOUSE_FLAGS} |= c{0} & 4 and !omm and MOUSE_MMB_DOWN;
                m{MOUSE_FLAGS} |= !(c{0} & 1) and olm and MOUSE_LMB_UP;
                m{MOUSE_FLAGS} |= !(c{0} & 2) and orm and MOUSE_RMB_UP;
                m{MOUSE_FLAGS} |= !(c{0} & 4) and omm and MOUSE_MMB_UP;
                if (m{MOUSE_FLAGS} & (MOUSE_LMB_DOWN | MOUSE_RMB_DOWN | MOUSE_MMB_DOWN)) focus_window(w);
                if (dragging or cursor_y <$ w.[WINDOW_Y] + TITLEBAR_SIZE)
                {
                    if (m{MOUSE_FLAGS} & MOUSE_LMB_DOWN)
                    {
                        dragging = w;
                        startx = cursor_x;
                        starty = cursor_y;
                    }
                    else if (m{MOUSE_FLAGS} & MOUSE_LMB_UP)
                    {
                        dragging = NULL;
                    }
                    else if (dragging)
                    {
                        ox = w.[WINDOW_X];
                        oy = w.[WINDOW_Y];
                        w.[WINDOW_X] += cursor_x - startx;
                        w.[WINDOW_Y] += cursor_y - starty;
                        framebuffer_refresh(mins(w.[WINDOW_X], ox), mins(w.[WINDOW_Y], oy), w.[WINDOW_W] + abs(ox - w.[WINDOW_X]), w.[WINDOW_H] + TITLEBAR_SIZE + abs(oy - w.[WINDOW_Y]), NULL);
                        startx = cursor_x;
                        starty = cursor_y;
                    }
                }
                else if (w{WINDOW_FLAGS} & WINDOW_FGET_MOUSE_EVENTS and (mouse_captured or (w.[WINDOW_Y] + TITLEBAR_SIZE <$ cursor_y)))
                {
                    if (!mouse_captured)
                    {
                        m.[MOUSE_X] = cursor_x - w.[WINDOW_X];
                        m.[MOUSE_Y] = cursor_y - w.[WINDOW_Y] - TITLEBAR_SIZE;
                    }
                    vec_push(&w{WINDOW_MOUSE_EVENTS}, m);
                    windows.[WINDOW_TASK].[TASK_WAIT] = FALSE;
                }
            }
            
            olm = c{0} & 1;
            orm = c{0} & 2;
            omm = c{0} & 4;
        }
    }
}

function seth(u)
(s{STRING}, p{VEC}, x, t, i)
{
    create_window(200, 200, 640, 480){WINDOW_FLAGS} = WINDOW_FGET_KEY_EVENTS | WINDOW_FPRINT_SCROLL;
    vec_init(p, 128, 1);
    s.[STRINGLEN] = 0;
    while (TRUE)
    {
.l:
        if (s.[STRINGLEN] == 0)
        {
            dir2path(p, &task{TASK_CWD});
            format(print, "%s> ", p);
            t = task.[TASK_WINDOW].[WINDOW_PRINT_BUFFER + VEC_LEN];
        }
        i = input(s, t, TRUE);
        if (!i)
        {
            clear();
            JMP .l
        }
        x = compile(&task{TASK_COMPILER}, "stdin", i);
        if (x)
        {
            s.[STRINGLEN] = 0;
            ans = x();
            format(print, "ans=%d (%x)\n", ans, ans);
        }
        else
        {
            if (task.[TASK_COMPILER + CCUR + TOKEN_TYPE] != TEND) s.[STRINGLEN] = 0;
        }
    }
}

function dir(p)
(f{FILE}, d{DIR_ENTRY}, s{STRING})
{
    if (!open(p, f, FALSE)) return format(print, "Could not open path \"%s\"\n", p);
    s.[STRINGLEN] = 11;
    s.[STRINGPTR] = &d{DIR_ENTRY_83NAME};
    while (dir_next(f, d))
    {
        format(print, "%s\n", s);
    }
}

function intcmp(x, y) return x[0] == y[0];
function hashint(x) return *x;
function memrep()
(m{MAP}, i, freemem, usedmem, n)
{
    map_init(m, 8, 4, 4, intcmp, hashint, NULL);
    i = first_free;
    freemem = 0;
    usedmem = 0;
    while (i)
    {
        if (!(i{MEM_HEADER_FLAGS} & MEM_HEADER_FUSED))
        {
            freemem += i.[MEM_HEADER_SIZE];
        }
        else
        {
            usedmem += i.[MEM_HEADER_SIZE];
            if (map_get(m, &i{MEM_HEADER_TASK}, &n))
            {
                n += i.[MEM_HEADER_SIZE];
                map_set(m, &i{MEM_HEADER_TASK}, &n);
            }
            else
            {
                map_set(m, &i{MEM_HEADER_TASK}, &i.[MEM_HEADER_SIZE]);
            }
        }
        i = i.[MEM_HEADER_NEXT];
    }
    format(print, "%dM free, %dK used, %dK reserved for YHWH\n", freemem /! 1024 /! 1024, usedmem /! 1024, (first_free - 0x500) /! 1024);
    n = 0;
    while (map_iter(m, &n))
    {
        format(print, "%dK used by task %s\n", n[1] /! 1024, &n[0]{TASK_NAME});
    }
    map_free(m);
}

function clear()
{
    task.[TASK_WINDOW].[WINDOW_PRINT_X] = 0;
    task.[TASK_WINDOW].[WINDOW_PRINT_Y] = 0;
    task.[TASK_WINDOW].[WINDOW_PRINT_CURSOR] = 0;
    task.[TASK_WINDOW].[WINDOW_PRINT_SCROLLTOP] = 0;
    vec_free(&task.[TASK_WINDOW]{WINDOW_PRINT_BUFFER});
    vec_init(&task.[TASK_WINDOW]{WINDOW_PRINT_BUFFER}, 128, 1);
    fill_rect(0, 0, task.[TASK_WINDOW].[WINDOW_W], task.[TASK_WINDOW].[WINDOW_H], 0xFFFFFF);
}

function edit(p)
(f{FILE}, s{STRING})
{
    clear();
    if (!open(p, f, TRUE)) return print("Could not open file\n");
    if (!vec_resize(&task.[TASK_WINDOW]{WINDOW_PRINT_BUFFER}, f.[FILE_SIZE]) or !read(f, f.[FILE_SIZE], task.[TASK_WINDOW].[WINDOW_PRINT_BUFFER + VEC_PTR])) return print("Could not read file\n");
    task.[TASK_WINDOW].[WINDOW_PRINT_BUFFER + VEC_LEN] = f.[FILE_SIZE];
    window_print_redraw(task.[TASK_WINDOW]);
    while (input(s, 0, TRUE));
    seek(f, 0);
    write(f, s.[STRINGLEN], s.[STRINGPTR]);
    resize(f, s.[STRINGLEN]);
    clear();
}

reboot:
    MOV EAX, 2
.r:
    IN AL, 0x64
    TEST AL, 2
    JNZ .r
    MOV AL, 0xFE
    OUT 0x64, AL
    CLI
.h: HLT
    JMP .h