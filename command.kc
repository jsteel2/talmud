import "/lib/exe.kc"
import "/lib/fs.kc"
import "/lib/print.kc"

function main(ign1, ign2, bootpath)
(s[STRING], buf{256}, argv[128 *! STRING], file{FILE}, cwd{FILE}, ocwd{FILE}, spl[STRING], pat[STRING], patbuf{256}, state, argc, n, o, cd, prog[STRING])
{
    print("\r
i i i i i i i      mmmmmmm   mm   m      m    m m    m mmmm  \r
I I I I I I I         #      ##   #      ##  ## #    # #   \"m\r
I  \\ `+' /  I         #     #  #  #      # ## # #    # #    #\r
 \\  `-+-'  /          #     #mm#  #      # \"\" # #    # #    #\r
  `-__|__-'           #    #    # #mmmmm #    # \"mmmm\" #mmm\" \r
      |\r
------+------\r
\r
Welcome to TalmudOS (Formerly ChucksOS) <COMMAND.EXE>\r
");

    if (!bootpath) bootpath = ":80;0/"; # we should get this from the bootloader instead but i dont have any space and i dont really care that much
    s[STRINGPTR] = buf;
    pat[STRINGPTR] = patbuf;
    pat[STRINGLEN] = bootpath[STRINGLEN];
    memcpy(patbuf, bootpath[STRINGPTR], pat[STRINGLEN], -1);
    if (!openat(0, pat, cwd, 0))
    {
        print("what in gods name?\r\n");
        CLI HLT
    }
    while (1)
    {
.c:
        format(print, "%s>", pat);
        input(s, 256);
        state = 0;
        argc = 0;
        while (strtok(s, ' ', spl, &state))
        {
            if (spl[STRINGLEN] != 0)
            {
                if (spl[STRINGPTR]{0} = '"')
                {
                    n = spl[STRINGPTR] - s[STRINGPTR] + 1;
                    o = n - 1;
                    while (n <! s[STRINGLEN] and s[STRINGPTR]{n} != '"') n = n + 1;
                    spl[STRINGPTR] = s[STRINGPTR] + o + 1;
                    spl[STRINGLEN] = n - o - 1;
                    state = n + 1;
                }
                argv[argc *! STRING + STRINGLEN] = spl[STRINGLEN];
                argv[argc *! STRING + STRINGPTR] = spl[STRINGPTR];
                argc = argc + 1;
            }
        }
        if (argc = 0) JMP .c
        if (stricmp(argv, "CD"))
        {
            if (argc = 1) cd = bootpath;
            else cd = argv + STRING *! 2;
            memcpy(ocwd, cwd, FILE, -1);
            if (!openat(cwd, cd, cwd, 0))
            {
                format(print, "Directory '%s' not found.\r\n", cd);
                memcpy(cwd, ocwd, FILE, -1);
            }
            else path_append(pat, cd);
            JMP .c
        }
        else if (stricmp(argv, "EXIT"))
        {
            return;
        }
        memcpy(prog, argv, STRING *! 2, -1);
        if (strchr(prog, '.') = prog[STRINGLEN])
        {
            memcpy(ocwd, prog[STRINGPTR], prog[STRINGLEN], -1);
            memcpy(ocwd + prog[STRINGLEN], ".EXE"[STRINGPTR], 4, -1);
            prog[STRINGLEN] = prog[STRINGLEN] + 4;
            prog[STRINGPTR] = ocwd;
        }
        if (!openat(cwd, prog, file, 0) and openat(cwd, "/BIN", file, 0) and !openat(file, prog, file, 0))
        {
            format(print, "File '%s' not found.\r\n", argv);
            JMP .c
        }
        exec(file, argc, argv, pat);
    }
}
